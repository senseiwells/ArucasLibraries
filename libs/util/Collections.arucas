import UnsupportedError from util.Errors;
import StringBuilder from util.StringUtils;

fun unsupported(message: String) {
    throw new UnsupportedError(message);
}

// Function that creates a new node
// cannot be accessed outside this file
newNode = null;
/* private */ {
    class Node {
        var value;
        var next;

        Node(value) {
            this.value = value;
            this.next = null;
        }

        fun toString() {
            return this.value.toString();
        }
    }

    newNode = fun(value) {
        return new Node(value);
    };
}

interface Stack {
    fun isEmpty();

    fun push(value);

    fun pop();

    fun peek();
}

interface Queue {
    fun isEmpty();

    fun enqueue(value);

    fun dequeue();

    fun front();

    fun end();
}

/**
 * Stack data strucutre using linked nodes
 * FIFO with basic methods
 */
class LinkedStack: Stack {
    var current;

    /**
     * Constructs a new LinkedStack
     */
    LinkedStack() {
        this.current = null;
    }

    /**
     * Checks if the stack is empty
     * Returns - Boolean: whether the stack is empty
     */
    fun isEmpty() {
        return this.current == null;
    }

    /**
     * This pushes a new value to the top of the stack
     * Parameter - Value (value): the value to add to the stack
     */
    fun push(value) {
        nextNode = newNode(value);
        nextNode.next = this.current;
        this.current = nextNode;
    }

    /**
     * This removes the value at the top of the stack
     * Returns - Value: the value that was at the top
     * Throws - Error: Stack is empty
     */
    fun pop() {
        if (this.isEmpty()) {
            throw new Error("Stack is empty");
        }
        currentNode = this.current;
        this.current = this.current.next;
        return currentNode.value;
    }

    /**
     * This peeks at the value at the top of the stack
     * Returns - Value: the value at the top of stack
     * Throws - Error: Stack is empty
     */
    fun peek() {
        if (this.isEmpty()) {
            throw new Error("Stack is empty");
        }
        return this.current.value;
    }

    /**
     * Converts the stack to a list,
     * with the top of the stack being first
     * Returns - List: the stack as a list
     */
    fun toList() {
        list = [];
        current = this.current;
        while (current != null) {
            list.append(current);
            current = current.next;
        }
        return list;
    }

    /**
     * Creates a string representation
     * of the stack
     * Returns - String: the string representation
     */
    fun toString() {
        builder = new StringBuilder("Stack{");
        current = this.current;
        while (current != null) {
            builder + current;
            current = current.next;
            if (current != null) {
                builder + ", ";
            }
        }
        return (builder + "}").toString();
    }
}

/**
 * Queue data strucutre using linked nodes
 * FILO with basic methods
 */
class LinkedQueue: Queue {
    /**
     * The node that is at the
     * front of the queue
     */
    var head;
    /**
     * The node that is at the
     * end of the queue
     */
    var tail;

    /**
     * Constructs a new LinkedQueue
     */
    LinkedQueue() {
        this.head = this.tail = null;
    }

    /**
     * Checks whether the queue is empty
     * Returns - Boolean: whether the queue is empty
     */
    fun isEmpty() {
        return this.head == null;
    }

    /**
     * Adds a value to the end of the queue
     * Parameter - Value (value): the value to add to the queue
     */
    fun enqueue(value) {
        nextNode = newNode(value);
        if (this.isEmpty()) {
            this.head = this.tail = nextNode;
        } else {
            this.tail = this.tail.next = nextNode;
        }
    }

    /**
     * Removes the value at the start of the queue
     * Returns - Value: the value at the start of the queue
     * Throws - Error: Queue is empty
     */
    fun dequeue() {
        if (this.isEmpty()) {
            throw new Error("Queue is empty");
        }
        head = this.head;
        this.head = this.head.next;
        return head.value;
    }

    /**
     * Gets the value at the front of the queue
     * Returns - Value: the value at the front of the queue
     * Throws - Error: Queue is empty
     */
    fun front() {
        if (this.isEmpty()) {
            throw new Error("Queue is empty");
        }
        return this.head.value;
    }

    /**
     * Gets the value at the end of the queue
     * Returns - Value: the value at the end of the queue
     * Throws - Error: Queue is empty
     */
    fun end() {
        if (this.isEmpty()) {
            throw new Error("Queue is empty");
        }
        return this.tail.value;
    }

    /**
     * Converts the queue to a list,
     * with the start of the queue being first
     * Returns - List: the queue as a list
     */
    fun toList() {
        list = [];
        head = this.head;
        while (head != null) {
            list.append(head);
            head = head.next;
        }
        return list;
    }

    /**
     * Creates a string representation
     * of the queue
     * Returns - String: the string representation
     */
    fun toString() {
        builder = new StringBuilder("Queue{");
        head = this.head;
        while (head != null) {
            builder + head;
            head = head.next;
            if (head != null) {
                builder + ", ";
            }
        }
        return (builder + "}").toString();
    }
}

class ImmutableList: List {
    fun set(object: Object, index: Number) unsupported("Cannot set in an immutable list");

    fun remove(index: Number) unsupported("Cannot remove from an immutable list");

    fun append(object: Object) unsupported("Cannot append to an immutable list");

    fun prepend(object: Object) unsupported("Cannot prepend to an immutable list");

    fun insert(object: Object, index: Number) unsupported("Cannot insert to an immutable list");

    fun addAll(collection: Collection) unsupported("Cannot add all to an immutable list");

    fun removeAll(collection: Collection) unsupported("Cannot remove all from an immutable list");

    fun retainAll(collection: Collection) unsupported("Cannot retain all from an immutable list");

    fun clear() unsupported("Cannot clear an immutable list");

    fun sort() unsupported("Cannot sort an immutable list");

    fun sort(comparator: Function) unsupported("Cannot sort an immutable list");

    fun reverse() unsupported("Cannot reverse an immutable list");

    fun shuffle() unsupported("Cannot shuffle an immutable list");

    fun copy() this;

    fun copyMutable() {
        return [].addAll(this);
    }
}
class DefaultedMap : Map {
    var defaultValue = null;

    DefaultedMap() : super() {}

    DefaultedMap(defaultValue) : super() {
        this.defaultValue = defaultValue;
    }

    DefaultedMap(items, defaultValue) : super() {
        this.defaultValue = defaultValue;
        foreach (item : items) {
            this.put(item, this.defaultValue);
        }
    }

    static fun createFrom(iterable : Iterable, defaultValue) {
        map = new DefaultedMap(defaultValue);
        map.addAll(iterable);
        return map;
    }

    fun addAll(values) {
        if (values.instanceOf(Iterable.type)) {
            foreach (item : values) {
                this.put(item, this.defaultValue);
            }
        }
        else {
            throw new Error("Type of values was not Iterable");
        }
    }
    fun addAll(values, specificValue) {
        if (values.instanceOf(Iterable.type)) {
            foreach (item : values) {
                this.put(item, specificValue);
            }
        }
        else {
            throw new Error("Type of values was not Iterable");
        }
    }
    fun put(key) {
        return this.put(key, this.defaultValue);
    }

    fun poll() {
        if (!this.isEmpty())
            return this.getKeys().get(0);
        return null;
    }

    fun get(ptr) {
        value = super.get(ptr);
        if (value == null) {
            return this.defaultValue
        }
    }
}

